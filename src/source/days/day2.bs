namespace day2

    function getLists(fileName as string) as integer[][]
        lines = files.getInputLines(fileName)
        data as integer[][] = []
        for each line in lines
            if line.Trim() = ""
                continue for
            end if
            data.push(files.getSpaceSeparatedInts(line))
        end for
        return data
    end function


    function checkLineForSafety(line as integer[], index as integer) as boolean
        safe = true
        lastDiff = 1
        for i = 1 to line.count() - 1
            diff = line[i] - line[i - 1]
            if diff = 0 or abs(diff) > 3
                safe = false
                logger.log([index, "unsafe for too big a jump"])
                exit for
            else if i > 1 and diff * lastDiff < 0
                safe = false
                logger.log([index, "unsafe for change pos/neg"])
                exit for
            end if
            lastDiff = diff
        end for
        if safe
            logger.log([index, `safe - ${lastDiff < 0 ? "decrease" : "increase"} `])
        end if
        return safe
    end function


    function checkLineCombosForSafety(line as integer[], index as integer) as boolean
        if checkLineForSafety(line, index)
            return true
        end if

        for i = 0 to line.count() - 1
            augLine = []
            augLine.append(line)
            augLine.Delete(i)
            if checkLineForSafety(augLine, index)
                return true
            end if
        end for
        return false
    end function


    function part1(fileName as string) as integer
        result = 0
        data = getLists(fileName)
        index = 0
        for each line in data
            if checkLineForSafety(line, index)
                result++
            end if
            index++
        end for

        return result
    end function

    function part2(fileName as string) as integer
        result = 0
        data = getLists(fileName)
        index = 0
        for each line in data
            if checkLineCombosForSafety(line, index)
                result++
            end if
            index++
        end for

        return result
    end function

end namespace
